//                              Apache License
//                        Version 2.0, January 2004
//                     http://www.apache.org/licenses/
// 
// TERMS AND CONDITIONS FOR USE, REPRODUCTION, AND DISTRIBUTION
// 
// 1. Definitions.
// 
//   "License" shall mean the terms and conditions for use, reproduction,
//   and distribution as defined by Sections 1 through 9 of this document.
// 
//   "Licensor" shall mean the copyright owner or entity authorized by
//   the copyright owner that is granting the License.
// 
//   "Legal Entity" shall mean the union of the acting entity and all
//   other entities that control, are controlled by, or are under common
//   control with that entity. For the purposes of this definition,
//   "control" means (i) the power, direct or indirect, to cause the
//   direction or management of such entity, whether by contract or
//   otherwise, or (ii) ownership of fifty percent (50%) or more of the
//   outstanding shares, or (iii) beneficial ownership of such entity.
// 
//   "You" (or "Your") shall mean an individual or Legal Entity
//   exercising permissions granted by this License.
// 
//   "Source" form shall mean the preferred form for making modifications,
//   including but not limited to software source code, documentation
//   source, and configuration files.
// 
//   "Object" form shall mean any form resulting from mechanical
//   transformation or translation of a Source form, including but
//   not limited to compiled object code, generated documentation,
//   and conversions to other media types.
// 
//   "Work" shall mean the work of authorship, whether in Source or
//   Object form, made available under the License, as indicated by a
//   copyright notice that is included in or attached to the work
//   (an example is provided in the Appendix below).
// 
//   "Derivative Works" shall mean any work, whether in Source or Object
//   form, that is based on (or derived from) the Work and for which the
//   editorial revisions, annotations, elaborations, or other modifications
//   represent, as a whole, an original work of authorship. For the purposes
//   of this License, Derivative Works shall not include works that remain
//   separable from, or merely link (or bind by name) to the interfaces of,
//   the Work and Derivative Works thereof.
// 
//   "Contribution" shall mean any work of authorship, including
//   the original version of the Work and any modifications or additions
//   to that Work or Derivative Works thereof, that is intentionally
//   submitted to Licensor for inclusion in the Work by the copyright owner
//   or by an individual or Legal Entity authorized to submit on behalf of
//   the copyright owner. For the purposes of this definition, "submitted"
//   means any form of electronic, verbal, or written communication sent
//   to the Licensor or its representatives, including but not limited to
//   communication on electronic mailing lists, source code control systems,
//   and issue tracking systems that are managed by, or on behalf of, the
//   Licensor for the purpose of discussing and improving the Work, but
//   excluding communication that is conspicuously marked or otherwise
//   designated in writing by the copyright owner as "Not a Contribution."
// 
//   "Contributor" shall mean Licensor and any individual or Legal Entity
//   on behalf of whom a Contribution has been received by Licensor and
//   subsequently incorporated within the Work.
// 
// 2. Grant of Copyright License. Subject to the terms and conditions of
//   this License, each Contributor hereby grants to You a perpetual,
//   worldwide, non-exclusive, no-charge, royalty-free, irrevocable
//   copyright license to reproduce, prepare Derivative Works of,
//   publicly display, publicly perform, sublicense, and distribute the
//   Work and such Derivative Works in Source or Object form.
// 
// 3. Grant of Patent License. Subject to the terms and conditions of
//   this License, each Contributor hereby grants to You a perpetual,
//   worldwide, non-exclusive, no-charge, royalty-free, irrevocable
//   (except as stated in this section) patent license to make, have made,
//   use, offer to sell, sell, import, and otherwise transfer the Work,
//   where such license applies only to those patent claims licensable
//   by such Contributor that are necessarily infringed by their
//   Contribution(s) alone or by combination of their Contribution(s)
//   with the Work to which such Contribution(s) was submitted. If You
//   institute patent litigation against any entity (including a
//   cross-claim or counterclaim in a lawsuit) alleging that the Work
//   or a Contribution incorporated within the Work constitutes direct
//   or contributory patent infringement, then any patent licenses
//   granted to You under this License for that Work shall terminate
//   as of the date such litigation is filed.
// 
// 4. Redistribution. You may reproduce and distribute copies of the
//   Work or Derivative Works thereof in any medium, with or without
//   modifications, and in Source or Object form, provided that You
//   meet the following conditions:
// 
//   (a) You must give any other recipients of the Work or
//       Derivative Works a copy of this License; and
// 
//   (b) You must cause any modified files to carry prominent notices
//       stating that You changed the files; and
// 
//   (c) You must retain, in the Source form of any Derivative Works
//       that You distribute, all copyright, patent, trademark, and
//       attribution notices from the Source form of the Work,
//       excluding those notices that do not pertain to any part of
//       the Derivative Works; and
// 
//   (d) If the Work includes a "NOTICE" text file as part of its
//       distribution, then any Derivative Works that You distribute must
//       include a readable copy of the attribution notices contained
//       within such NOTICE file, excluding those notices that do not
//       pertain to any part of the Derivative Works, in at least one
//       of the following places: within a NOTICE text file distributed
//       as part of the Derivative Works; within the Source form or
//       documentation, if provided along with the Derivative Works; or,
//       within a display generated by the Derivative Works, if and
//       wherever such third-party notices normally appear. The contents
//       of the NOTICE file are for informational purposes only and
//       do not modify the License. You may add Your own attribution
//       notices within Derivative Works that You distribute, alongside
//       or as an addendum to the NOTICE text from the Work, provided
//       that such additional attribution notices cannot be construed
//       as modifying the License.
// 
//   You may add Your own copyright statement to Your modifications and
//   may provide additional or different license terms and conditions
//   for use, reproduction, or distribution of Your modifications, or
//   for any such Derivative Works as a whole, provided Your use,
//   reproduction, and distribution of the Work otherwise complies with
//   the conditions stated in this License.
// 
// 5. Submission of Contributions. Unless You explicitly state otherwise,
//   any Contribution intentionally submitted for inclusion in the Work
//   by You to the Licensor shall be under the terms and conditions of
//   this License, without any additional terms or conditions.
//   Notwithstanding the above, nothing herein shall supersede or modify
//   the terms of any separate license agreement you may have executed
//   with Licensor regarding such Contributions.
// 
// 6. Trademarks. This License does not grant permission to use the trade
//   names, trademarks, service marks, or product names of the Licensor,
//   except as required for reasonable and customary use in describing the
//   origin of the Work and reproducing the content of the NOTICE file.
// 
// 7. Disclaimer of Warranty. Unless required by applicable law or
//   agreed to in writing, Licensor provides the Work (and each
//   Contributor provides its Contributions) on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
//   implied, including, without limitation, any warranties or conditions
//   of TITLE, NON-INFRINGEMENT, MERCHANTABILITY, or FITNESS FOR A
//   PARTICULAR PURPOSE. You are solely responsible for determining the
//   appropriateness of using or redistributing the Work and assume any
//   risks associated with Your exercise of permissions under this License.
// 
// 8. Limitation of Liability. In no event and under no legal theory,
//   whether in tort (including negligence), contract, or otherwise,
//   unless required by applicable law (such as deliberate and grossly
//   negligent acts) or agreed to in writing, shall any Contributor be
//   liable to You for damages, including any direct, indirect, special,
//   incidental, or consequential damages of any character arising as a
//   result of this License or out of the use or inability to use the
//   Work (including but not limited to damages for loss of goodwill,
//   work stoppage, computer failure or malfunction, or any and all
//   other commercial damages or losses), even if such Contributor
//   has been advised of the possibility of such damages.
// 
// 9. Accepting Warranty or Additional Liability. While redistributing
//   the Work or Derivative Works thereof, You may choose to offer,
//   and charge a fee for, acceptance of support, warranty, indemnity,
//   or other liability obligations and/or rights consistent with this
//   License. However, in accepting such obligations, You may act only
//   on Your own behalf and on Your sole responsibility, not on behalf
//   of any other Contributor, and only if You agree to indemnify,
//   defend, and hold each Contributor harmless for any liability
//   incurred by, or claims asserted against, such Contributor by reason
//   of your accepting any such warranty or additional liability.
// 
// END OF TERMS AND CONDITIONS
// 
// APPENDIX: How to apply the Apache License to your work.
// 
//   To apply the Apache License to your work, attach the following
//   boilerplate notice, with the fields enclosed by brackets "{}"
//   replaced with your own identifying information. (Don't include
//   the brackets!)  The text should be enclosed in the appropriate
//   comment syntax for the file format. We also recommend that a
//   file or class name and description of purpose be included on the
//   same "printed page" as the copyright notice for easier
//   identification within third-party archives.
// 
// Copyright {yyyy} {name of copyright owner}
// 
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
// 
//    http://www.apache.org/licenses/LICENSE-2.0
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//
// This shader used to bootstrap Nu's physically-based renderer was taken from -
// https://github.com/TheCherno/Sparky/blob/a679d0834e37eb3570dff18b01550210734cb97e/Sandbox/shaders/AdvancedLighting.shader

#shader vertex
#version 330 core

layout(location = 0) in vec4 position;
layout(location = 1) in vec3 normal;
layout(location = 2) in vec2 uv;
layout(location = 3) in vec3 binormal;
layout(location = 4) in vec3 tangent;

uniform mat4 sys_ProjectionMatrix;
uniform mat4 sys_ViewMatrix;
uniform mat4 sys_ModelMatrix;
uniform vec3 sys_CameraPosition;

uniform mat4 u_DepthBiasMVP;

out DATA
{
	vec4 position;
	vec3 normal;
	vec2 uv;
	vec3 binormal;
	vec3 tangent;
	vec3 color;
	vec4 shadowCoord;
	vec3 cameraPos;
} vs_out;

void main()
{
	vec4 pos = sys_ModelMatrix * position;
	vs_out.position = pos;
	gl_Position = sys_ProjectionMatrix * sys_ViewMatrix * pos;

	mat3 model = mat3(sys_ModelMatrix);
	vs_out.normal = model * normal;
	vs_out.binormal = model * binormal;
	vs_out.tangent = model * tangent;
	vs_out.uv = uv;
	vs_out.color = vec3(1.0);
	vs_out.cameraPos = sys_CameraPosition;

	vs_out.shadowCoord = u_DepthBiasMVP * pos;
}

#shader fragment
#version 330 core

layout(location = 0) out vec4 color;

in DATA
{
	vec4 position;
	vec3 normal;
	vec2 uv;
	vec3 binormal;
	vec3 tangent;
	vec3 color;
	vec4 shadowCoord;
	vec3 cameraPos;
} fs_in;

struct Light
{
	vec4 color;
	vec3 position;
	float p0;
	vec3 direction;
	float p1;
	vec3 lightVector;
	float intensity;
};

struct Material
{
	vec4 albedo;
	vec3 specular;
	float roughness;
	vec3 normal;
};

struct Attributes
{
	vec3 position;
	vec3 normal;
	vec3 binormal;
	vec3 tangent;
};

uniform Light sys_Light;

// PBR Inputs
uniform sampler2D u_PreintegratedFG;
uniform samplerCube u_EnvironmentMap;

// PBR Map Inputs
uniform sampler2D u_AlbedoMap;
uniform sampler2D u_SpecularMap;
uniform sampler2D u_GlossMap;
uniform sampler2D u_NormalMap;

uniform sampler2DShadow u_ShadowMap;

// PBR Static Inputs
uniform vec4 u_AlbedoColor;
uniform vec3 u_SpecularColor;
uniform float u_GlossColor;
uniform vec3 u_NormalColor;

// PBR Modes
uniform float u_UsingAlbedoMap;
uniform float u_UsingSpecularMap;
uniform float u_UsingGlossMap;
uniform float u_UsingNormalMap;

#define PI 3.1415926535897932384626433832795
#define GAMMA 2.2

vec2 poissonDisk[16] = vec2[](
	vec2(-0.94201624, -0.39906216),
	vec2(0.94558609, -0.76890725),
	vec2(-0.094184101, -0.92938870),
	vec2(0.34495938, 0.29387760),
	vec2(-0.91588581, 0.45771432),
	vec2(-0.81544232, -0.87912464),
	vec2(-0.38277543, 0.27676845),
	vec2(0.97484398, 0.75648379),
	vec2(0.44323325, -0.97511554),
	vec2(0.53742981, -0.47373420),
	vec2(-0.26496911, -0.41893023),
	vec2(0.79197514, 0.19090188),
	vec2(-0.24188840, 0.99706507),
	vec2(-0.81409955, 0.91437590),
	vec2(0.19984126, 0.78641367),
	vec2(0.14383161, -0.14100790)
	);

Attributes g_Attributes;

vec4 GammaCorrectTexture(sampler2D tex, vec2 uv)
{
	vec4 samp = texture(tex, uv);
	return vec4(pow(samp.rgb, vec3(GAMMA)), samp.a);
}

vec3 GammaCorrectTextureRGB(sampler2D tex, vec2 uv)
{
	vec4 samp = texture(tex, uv);
	return vec3(pow(samp.rgb, vec3(GAMMA)));
}

vec4 GetAlbedo()
{
	return (1.0 - u_UsingAlbedoMap) * u_AlbedoColor + u_UsingAlbedoMap * GammaCorrectTexture(u_AlbedoMap, fs_in.uv);
}

vec3 GetSpecular()
{
	return (1.0 - u_UsingSpecularMap) * u_SpecularColor + u_UsingSpecularMap * GammaCorrectTextureRGB(u_SpecularMap, fs_in.uv);
}

float GetGloss()
{
	return (1.0 - u_UsingGlossMap) * u_GlossColor + u_UsingGlossMap * GammaCorrectTextureRGB(u_GlossMap, fs_in.uv).r;
}

float GetRoughness()
{
	return 1.0 - GetGloss();
}

vec3 FinalGamma(vec3 color)
{
	return pow(color, vec3(1.0 / GAMMA));
}

float FresnelSchlick(float f0, float fd90, float view)
{
	return f0 + (fd90 - f0) * pow(max(1.0 - view, 0.1), 5.0);
}

float Disney(Light light, Material material, vec3 eye)
{
	vec3 halfVector = normalize(light.lightVector + eye);

	float NdotL = max(dot(g_Attributes.normal, light.lightVector), 0.0);
	float LdotH = max(dot(light.lightVector, halfVector), 0.0);
	float NdotV = max(dot(g_Attributes.normal, eye), 0.0);

	float energyBias = mix(0.0, 0.5, material.roughness);
	float energyFactor = mix(1.0, 1.0 / 1.51, material.roughness);
	float fd90 = energyBias + 2.0 * (LdotH * LdotH) * material.roughness;
	float f0 = 1.0;

	float lightScatter = FresnelSchlick(f0, fd90, NdotL);
	float viewScatter = FresnelSchlick(f0, fd90, NdotV);

	return lightScatter * viewScatter * energyFactor;
}

vec3 GGX(Light light, Material material, vec3 eye)
{
	vec3 h = normalize(light.lightVector + eye);
	float NdotH = max(dot(g_Attributes.normal, h), 0.0);

	float rough2 = max(material.roughness * material.roughness, 2.0e-3); // capped so spec highlights don't disappear
	float rough4 = rough2 * rough2;

	float d = (NdotH * rough4 - NdotH) * NdotH + 1.0;
	float D = rough4 / (PI * (d * d));

	// Fresnel
	vec3 reflectivity = material.specular;
	float fresnel = 1.0;
	float NdotL = clamp(dot(g_Attributes.normal, light.lightVector), 0.0, 1.0);
	float LdotH = clamp(dot(light.lightVector, h), 0.0, 1.0);
	float NdotV = clamp(dot(g_Attributes.normal, eye), 0.0, 1.0);
	vec3 F = reflectivity + (fresnel - fresnel * reflectivity) * exp2((-5.55473 * LdotH - 6.98316) * LdotH);

	// geometric / visibility
	float k = rough2 * 0.5;
	float G_SmithL = NdotL * (1.0 - k) + k;
	float G_SmithV = NdotV * (1.0 - k) + k;
	float G = 0.25 / (G_SmithL * G_SmithV);

	return G * D * F;
}

vec3 RadianceIBLIntegration(float NdotV, float roughness, vec3 specular)
{
	vec2 preintegratedFG = texture(u_PreintegratedFG, vec2(roughness, 1.0 - NdotV)).rg;
	return specular * preintegratedFG.r + preintegratedFG.g;
}

vec3 IBL(Light light, Material material, vec3 eye)
{
	float NdotV = max(dot(g_Attributes.normal, eye), 0.0);

	vec3 reflectionVector = normalize(reflect(-eye, g_Attributes.normal));
	float smoothness = 1.0 - material.roughness;
	float mipLevel = (1.0 - smoothness * smoothness) * 10.0;
	vec4 cs = textureLod(u_EnvironmentMap, reflectionVector, mipLevel);
	vec3 result = pow(cs.xyz, vec3(GAMMA)) * RadianceIBLIntegration(NdotV, material.roughness, material.specular);

	vec3 diffuseDominantDirection = g_Attributes.normal;
	float diffuseLowMip = 9.6;
	vec3 diffuseImageLighting = textureLod(u_EnvironmentMap, diffuseDominantDirection, diffuseLowMip).rgb;
	diffuseImageLighting = pow(diffuseImageLighting, vec3(GAMMA));

	return result + diffuseImageLighting * material.albedo.rgb;
}

float Diffuse(Light light, Material material, vec3 eye)
{
	return Disney(light, material, eye);
}

vec3 Specular(Light light, Material material, vec3 eye)
{
	return GGX(light, material, eye);
}

// TODO: Windowing
float Attenuate(Light light)
{
	vec3 direction = light.position - vec3(fs_in.position);
	return light.intensity * 1.0 / (dot(direction, direction) + 0.01); // TODO: 4pi
}

float random(vec3 seed, int i)
{
	vec4 seed4 = vec4(seed, i);
	float dot_product = dot(seed4, vec4(12.9898, 78.233, 45.164, 94.673));
	return fract(sin(dot_product) * 43758.5453);
}

vec3 NormalMap()
{
	mat3 toWorld = mat3(g_Attributes.binormal, g_Attributes.tangent, g_Attributes.normal);
	vec3 normalMap = texture(u_NormalMap, fs_in.uv).rgb * 2.0 - 1.0;
	normalMap = toWorld * normalMap.rgb;
	normalMap = normalize(normalMap);
	return normalMap;
}

void main()
{
	g_Attributes.position = fs_in.position.xyz;
	// Normalize inputs
	g_Attributes.normal = normalize(fs_in.normal);
	g_Attributes.tangent = normalize(fs_in.tangent);
	g_Attributes.binormal = normalize(fs_in.binormal);

	if (u_UsingNormalMap > 0.0)
		g_Attributes.normal = NormalMap();

	vec3 eye = normalize(fs_in.cameraPos - g_Attributes.position);

	Light light = sys_Light;
	light.intensity = light.intensity; // Attenuate(light);
	light.lightVector = light.direction; // normalize(light.position - vec3(fs_in.position));

	Material material;
	material.albedo = GetAlbedo();
	material.specular = GetSpecular();
	material.roughness = GetRoughness();

	vec4 diffuse = vec4(0.0);
	vec3 specular = vec3(0.0);

	// TODO: Multiple lights
	for (int i = 0; i < 1; i++)
	{
		float NdotL = clamp(dot(g_Attributes.normal, light.lightVector), 0.0, 1.0);

		// Diffuse Calculation
		diffuse += NdotL * Diffuse(light, material, eye) * light.color * light.intensity;
		// Specular Calculation
		specular += NdotL * Specular(light, material, eye) * light.color.xyz * light.intensity;
		light.intensity /= 2.0;
		light.lightVector = -light.lightVector;
	}
	// Shadows
	float bias = 0.005;
	float visibility = 1.0;
	for (int i = 0; i < 1; i++)
	{
		int index = int(16.0 * random(floor(fs_in.position.xyz * 1000.0), i)) % 16;
		visibility -= (1.0 / 4.0) * (1.0 - texture(u_ShadowMap, vec3(fs_in.shadowCoord.xy + poissonDisk[index] / 700.0, (fs_in.shadowCoord.z - bias) / fs_in.shadowCoord.w)));
	}

	vec3 finalColor = material.albedo.rgb * diffuse.rgb * visibility + (specular + IBL(light, material, eye)) * visibility;
	finalColor = FinalGamma(finalColor);
	color = vec4(finalColor, material.albedo.a);
}